#
# A simple theme that only shows relevant information.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Dominik Liebler <liebler.dominik@googlemail.com>
#

# Load dependencies
pmodload 'helper'

function prompt_domnikl_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_domnikl_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  add-zsh-hook precmd prompt_domnikl_precmd

  zstyle ':prezto:module:editor' completing '%B%F{red} ...%f%b'
  zstyle ':prezto:module:editor:keymap' alternate ' %F{yellow}❮%f%B%F{red}❮%f%b%F{red}❮%f'
  zstyle ':prezto:module:git' added ' %%B%F{green}✚%f%%b'
  zstyle ':prezto:module:git' branch ':%F{yellow}%b%f'
  zstyle ':prezto:module:git' deleted ' %%B%F{red}✖%f%%b'
  zstyle ':prezto:module:git' modified ' %%B%F{blue}✱%f%%b'
  zstyle ':prezto:module:git' renamed ' %%B%F{magenta}➜%f%%b'
  zstyle ':prezto:module:git' untracked ' %%B%F{white}◼%f%%b'
  zstyle ':prezto:module:git' info \
    'prompt'  ' $(coalesce "%b" "%p" "%c")%s' \
    'rprompt' '%A%B%S%a%d%m%r%U%u'

  PROMPT='%F{green}%n%f %F{cyan}%1~%f${git_info:+${(e)git_info[prompt]}} %(!.%B%F{red}#%f%b.%B%F{white}❯%f%b) '
  RPROMPT='${editor_info[keymap]}${editor_info[overwrite]}%(?:: %F{red}⏎%f)${VIM:+" %B%F{green}V%f%b"}${git_info[rprompt]}%(1j. %j.)'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_domnikl_setup "$@"
